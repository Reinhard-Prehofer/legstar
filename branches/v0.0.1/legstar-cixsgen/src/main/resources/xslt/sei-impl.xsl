<?xml version="1.0" encoding="UTF-8"?>
<!-- ===============================================================================================
   XSLT for SEI implementation Generation. The class performs the host marshalling
   calls the backend system and unmarshalls the reply.
 -->
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:output method="text" omit-xml-declaration="yes" indent="yes"/>
<xsl:template match="/"><xsl:apply-templates select="cixs-service" /></xsl:template>

<!-- Generate the service endpoint implementation class -->
<xsl:template match="cixs-service">

  <!-- Determine the endpoint implementation java source file name -->
  <xsl:variable name="implementation-class-name">
    <xsl:choose>
      <xsl:when test="string-length(endpoint-implementation) > 0"><xsl:value-of select="endpoint-implementation"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="concat(upper-case(substring(service-name,1,1)),substring(service-name,2))"/>Impl</xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="target-dir">
    <xsl:value-of select="translate(service-endpoint-package,'.','/')"/>
  </xsl:variable>
  
  <!-- Generate the dynamically built java source file -->
  <xsl:result-document href="{$target-dir}/{$implementation-class-name}.java" method="text" omit-xml-declaration="yes" indent="yes">
    <xsl:call-template name="generate-header"/>
    <xsl:call-template name="generate-class">
      <xsl:with-param name="implementation-class-name"><xsl:value-of select="$implementation-class-name"/></xsl:with-param>
    </xsl:call-template>
  </xsl:result-document>

</xsl:template>

<!-- ===============================================================================================
   Generate the package and import code
 -->
<xsl:template name="generate-header">
package <xsl:value-of select="service-endpoint-package"/>;
import javax.jws.WebService;
import com.legstar.cixs.coxb.CIXSParameter;
import com.legstar.cixs.coxb.ICIXSInvoker;
import com.legstar.cixs.coxb.CIXSInvokerFactory;
import com.legstar.cixs.coxb.CIXSHeader;
import com.legstar.cixs.coxb.CIXSException;
<xsl:for-each select="cixs-operation">

<!-- Import the operation wrapper objects packages -->
<xsl:if test="string-length(operation-package) > 0 and (operation-package != ../service-endpoint-package)">
import <xsl:value-of select="operation-package"/>.*;</xsl:if>
</xsl:for-each>

/**
 * Web service enpoint implementation.
 * 
 * This class was generated by CIXS version 1.0.
 * <xsl:value-of  select="current-dateTime()"/>
 */
</xsl:template>

<!-- ===============================================================================================
   Generate the code of the java class that implements the SEI
 -->
<xsl:template name="generate-class">
<xsl:param name="implementation-class-name"/>
  <xsl:variable name="sei-class-name">
    <xsl:choose>
      <xsl:when test="string-length(endpoint-interface) > 0"><xsl:value-of select="endpoint-interface"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="concat(upper-case(substring(service-name,1,1)),substring(service-name,2))"/></xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="wsdl-service-name">
    <xsl:choose>
      <xsl:when test="string-length(wsdl-service-name) > 0"><xsl:value-of select="wsdl-service-name"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="service-name"/>Service</xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="hostheader-class-name">
    <xsl:choose>
      <xsl:when test="string-length(endpoint-interface) > 0"><xsl:value-of select="endpoint-interface"/>HostHeader</xsl:when>
      <xsl:otherwise><xsl:value-of select="concat(upper-case(substring(service-name,1,1)),substring(service-name,2))"/>HostHeader</xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
@WebService(endpointInterface = "<xsl:value-of select="service-endpoint-package"/>.<xsl:value-of select="$sei-class-name"/>",
        serviceName = "<xsl:value-of select="$wsdl-service-name"/>",
        targetNamespace = "<xsl:value-of select="service-targetnamespace"/>")
public class <xsl:value-of select="$implementation-class-name"/> implements <xsl:value-of select="$sei-class-name"/> {

  /** The input parameter set for the invoke method. */
  private CIXSParameter mInParameter;
  
  /** The output parameter set for the invoke method. */
  private CIXSParameter mOutParameter;

  /** The invoker object. */
  private ICIXSInvoker mInvoker;

<xsl:for-each select="cixs-operation">

  <xsl:variable name="operation-class-name">
    <xsl:value-of select="concat(upper-case(substring(operation-name,1,1)),substring(operation-name,2))"/>
  </xsl:variable>
  <xsl:variable name="operation-package">
    <xsl:choose>
      <xsl:when test="string-length(operation-package) > 0"><xsl:value-of select="operation-package"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="../service-endpoint-package"/></xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="operation-namespace">
    <xsl:choose>
      <xsl:when test="string-length(operation-targetnamespace) > 0"><xsl:value-of select="operation-targetnamespace"/></xsl:when>
      <xsl:otherwise><xsl:value-of select="../service-targetnamespace"/></xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="fault-type">
    <xsl:choose>
      <xsl:when test="string-length(fault-type) > 0"><xsl:value-of select="fault-type"/></xsl:when>
        <xsl:otherwise><xsl:value-of select="$operation-class-name"/>Fault</xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  <xsl:variable name="fault-info-type">
    <xsl:choose>
      <xsl:when test="string-length(fault-info-type) > 0"><xsl:value-of select="fault-info-type"/></xsl:when>
        <xsl:otherwise><xsl:value-of select="$operation-class-name"/>FaultInfo</xsl:otherwise>
    </xsl:choose>
  </xsl:variable>
  /**
   * Contructor gets an invoker object.
   * @throws <xsl:value-of select="$fault-type"/> if fails to get invoker 
   */
  public <xsl:value-of select="$implementation-class-name"/>() throws <xsl:value-of select="$fault-type"/> {
    CIXSInvokerFactory cf = new CIXSInvokerFactory();
    try {
        mInvoker = cf.createInvoker();
        mInParameter = mInvoker.createParameter();
        mOutParameter = mInvoker.createParameter();
    } catch (CIXSException e) {
        report<xsl:value-of select="$fault-type"/>Exception(e, "Failed to create an invoker");
    }
  }
  
  /** {@inheritDoc} */
  public final <xsl:value-of select="output/@jaxb-package"/>.
          <xsl:value-of select="output/@jaxb-type"/><xsl:text> </xsl:text><xsl:value-of select="operation-name"/>(
          final <xsl:value-of select="input/@jaxb-package"/>.
                <xsl:value-of select="input/@jaxb-type"/> request,
          final <xsl:value-of select="$sei-class-name"/>HostHeader hostHeader)
      throws <xsl:value-of select="$fault-type"/> {
    
    /* The JAXB input factory. */
    <xsl:value-of select="input/@jaxb-package"/>.ObjectFactory jaxbInFactory =
          new <xsl:value-of select="input/@jaxb-package"/>.ObjectFactory(); 
    
    /* The JAXB output factory. */
    <xsl:value-of select="output/@jaxb-package"/>.ObjectFactory jaxbOutFactory =
          new <xsl:value-of select="output/@jaxb-package"/>.ObjectFactory();  
    
    <xsl:value-of select="output/@jaxb-package"/>.
    <xsl:value-of select="output/@jaxb-type"/> reply = null;
    
    try {
              
      /* Initialize invoker with static data and data from headers */
      mInvoker.initialize(getHostParameters(hostHeader), "<xsl:value-of select="operation-name"/>");

      /* Prepare the input parameter set using static binding */
      <xsl:value-of select="input/@jaxb-package"/>.bind.
        <xsl:value-of select="input/@jaxb-type"/>Binding cein =
          new <xsl:value-of select="input/@jaxb-package"/>.bind.
              <xsl:value-of select="input/@jaxb-type"/>Binding(jaxbInFactory, request);
      mInParameter.setComplexBinding(cein);
      
      /* Prepare the output parameter set using static binding */
      <xsl:value-of select="output/@jaxb-package"/>.bind.
      <xsl:value-of select="output/@jaxb-type"/>Binding ceout =
          new <xsl:value-of select="output/@jaxb-package"/>.bind.
              <xsl:value-of select="output/@jaxb-type"/>Binding(jaxbOutFactory);
      mOutParameter.setComplexBinding(ceout);
      
      /* Call remote program */
      mInvoker.invoke(mInParameter, mOutParameter);
      
      /* Get reply object */
      reply = ceout.getJaxbObject(); 
      
    } catch (CIXSException e) {
      report<xsl:value-of select="$fault-type"/>Exception(e,
          "Failed to invoke host program:");
    }

    return reply;
  }

  /**
   * Formats a fault element to notify client of an exception.
   *
   * @param e the exception which occured
   * @param text short message describing the context
   * @throws <xsl:value-of select="$fault-type"/> the fault exception
   */
  private void report<xsl:value-of select="$fault-type"/>Exception(
      final Exception e,
      final String text) throws <xsl:value-of select="$fault-type"/> {
    e.printStackTrace();
    <xsl:value-of select="$fault-info-type"/> faultInfo = new <xsl:value-of select="$fault-info-type"/>();
    faultInfo.setMessage(e.getMessage());
    faultInfo.setDetail("Operation="
            + "<xsl:value-of select="$operation-class-name"/>"
            + " Package="
            + "<xsl:value-of select="$operation-package"/>");
    throw (new <xsl:value-of select="$fault-type"/>(text + ' ' 
            + faultInfo.getMessage(), faultInfo));
    
  }

</xsl:for-each>
  /**
   * Extracts header data from SOAP header.
   * 
   * @param hostHeader the JAXB object mapping the SOAP header element
   * @return the header data
   */
  private CIXSHeader getHostParameters(
          final <xsl:value-of select="$hostheader-class-name"/> hostHeader) {
    if (hostHeader == null) {
        return null;
    }
    CIXSHeader ch = new CIXSHeader();
    ch.setHostUser(hostHeader.getHostUser());
    ch.setHostPassword(hostHeader.getHostPassword());
    ch.setHostIPAddress(hostHeader.getHostIPAddress());
    ch.setHostIPPort(hostHeader.getHostIPPort());
    ch.setHostCICWPath(hostHeader.getHostCICWPath());
    return ch;
  }
  
}
</xsl:template>

</xsl:stylesheet>
